buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}


apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "0.1.0"
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
    }

    def secretFile = file("${rootDir}/secret.gradle")
    if (secretFile.exists()) {
        apply from: secretFile.absolutePath
        signingConfigs {
            release {
                storeFile project.ext.storeFile
                storePassword project.ext.storePassword
                keyAlias project.ext.keyAlias
                keyPassword project.ext.keyPassword
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }

        release {
            if (secretFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:support-v13:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'

    compile 'com.jakewharton.timber:timber:2.4.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.google.code.gson:gson:2.3.1'

    compile 'io.reactivex:rxjava:1.0.3'
    compile 'com.ogaclejapan:rxbinding:1.0'

    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'com.makeramen:roundedimageview:1.5.0'
    compile 'com.balysv.materialmenu:material-menu:1.5.0'
    compile 'org.jsoup:jsoup:1.8.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
        transitive = true;
    }
}

