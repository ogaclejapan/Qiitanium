apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

android {

  compileSdkVersion COMPILE_SDK_VERSION as int
  buildToolsVersion BUILD_TOOLS_VERSION

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion COMPILE_SDK_VERSION as int
    versionCode VERSION_CODE as int
    versionName VERSION_NAME
  }

  def secretFile = file("${rootDir}/secret.gradle")
  if (secretFile.exists()) {
    apply from: secretFile.absolutePath
    signingConfigs {
      release {
        storeFile project.ext.storeFile
        storePassword project.ext.storePassword
        keyAlias project.ext.keyAlias
        keyPassword project.ext.keyPassword
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      if (secretFile.exists()) {
        signingConfig signingConfigs.release
      }
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }

  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

}

dependencies {

  compile fileTree(dir: 'libs', include: ['*.jar'])

  // Android support library
  compile 'com.android.support:appcompat-v7:22.1.+'
  compile 'com.android.support:support-v4:22.1.+'
  compile 'com.android.support:support-v13:22.0.+'

  // DI
  compile 'com.google.dagger:dagger:2.0'
  apt 'com.google.dagger:dagger-compiler:2.0'
  provided 'org.glassfish:javax.annotation:10.0-b28'

  // Logger
  compile 'com.jakewharton.timber:timber:2.7.1'

  // HTTP client
  compile 'com.squareup.okhttp:okhttp:2.3.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'

  // Image downloader & transformer
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.makeramen:roundedimageview:2.0.1'

  // JSON library
  compile 'com.google.code.gson:gson:2.3.1'

  // Reactive library
  compile 'io.reactivex:rxjava:1.0.10'
  compile 'com.ogaclejapan:rxbinding:1.1.1@aar'

  // View library
  compile 'com.ogaclejapan.arclayout:library:1.0.1@aar'
  compile 'com.ogaclejapan.smarttablayout:library:1.1.3@aar'
  compile 'com.ogaclejapan.smarttablayout:utils-v13:1.1.3@aar'
  compile 'com.twotoasters.jazzylistview:library:1.2.1'
  compile 'com.sothree.slidinguppanel:library:3.0.0@aar'
  compile 'com.nispok:snackbar:2.10.8'

  // Custom font library
  compile 'com.norbsoft.typefacehelper:library:0.9.0@aar'

  // HTML Parser
  compile 'org.jsoup:jsoup:1.8.1'

  // Crash analytics
  compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
    transitive = true;
  }

  //Debug tools
  debugCompile 'com.facebook.stetho:stetho:1.1.1'
  debugCompile 'com.facebook.stetho:stetho-okhttp:1.1.1'

  
}

